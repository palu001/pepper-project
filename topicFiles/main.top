topic: ~cinema_assistant()
language: enu

# --- Concepts ---
concept: (myname) ["my name is" "i'm" "i am" "call me" "they call me" "you can call me" "i go by"]
concept: (greetings) ^rand["hi" "hello" "hello there" "hey there" "good morning" "hey" "hi there" "good afternoon" "good evening" "greetings" "good day"]
concept: (goodbye) ["goodbye" "bye" "farewell" "see you later" "take care"]
concept: (acceptance) ["yes" "yeah" "yep" "ok" "sure" "of course" "sounds good" "alright"]
concept: (refusal) ["no" "nope" "nah" "not now" "maybe later" "i'm good"]
concept: (movie_genres) ["action" "comedy" "drama" "horror" "romance" "sci-fi" "science fiction" "thriller" "documentary" "animated" "family" "adventure" "fantasy"]
concept: (age_groups) ["child" "teen"  "adult" "senior"]
concept: (directions) ["bathroom" "restroom" "washroom" "screen 1" "screen 2" "screen 3" "screen 4" "screen 5" "screen 6" "screen 7" "screen 8" "theater" "auditorium" "concession" "concession stand" "exit" "entrance" "box office" "ticket office"]
concept: (concessions) ["popcorn" "soda" "drink" "beverage" "nachos" "candy" "sweets" "snack" "hot dog" "food"]
concept: (update_prefs) ["update" "preferences" "change" "info" "details" "profile"]
concept: (ask_recommendation) ["recommend" "watch" "recommendations" "recommendation" "see"]
concept: (ask_showtimes) ["showtimes" "movie times" "what time are the movies playing" "get showtimes" "what's playing"]
concept: (ask_directions) ["directions" "where" "get to" "where is"]
concept: (ask_concessions) ["concessions" "buy snacks" "order food" "what food do you have" "menu" "i'm hungry"]
concept: (done) ["i'm done" "i am done" "done" "all done" "that's it" "finished" "i'm finished" "i’m good now"]
concept: (emergency) ["help" "emergency" "i need help now" "assistance" "urgent help"]

# --- Global Greeting ---   
u: (~greetings)
    Hello! Welcome to our cinema.
    ^gotoReactivate(get_name)

# --- Global Goodbye (Capire se rimuovere o meno)
u: (~goodbye)
    Thank you for visiting!
    $cinema/function = restart
    u1: (e:cinema/restart)
        ^gotoReactivate(restart)

# --- Restart --- 
proposal: %restart
    u1: (~greetings) ^gotoReactivate(get_name)


# --- Get Name ---
proposal: %get_name
    What's your name?

    u1: ({~myname} _*)
        $cinema/customer_name=$1
        $cinema/function = greet_customer
        u2: (e:cinema/customer_identity_check $cinema/customer_identity_check==True) 
            Great! Welcome back, $cinema/customer_name
                ^gotoReactivate(main_hub)
        u2: (e:cinema/customer_identity_check $cinema/customer_identity_check==False) 
                Nice to meet you, $cinema/customer_name! Let's get to know you better.
                ^gotoReactivate(get_age)
                

# --- Get Age Group ---
proposal: %get_age
    What age group do you belong to? child, teen, adult, or senior?

    u1: ({*} _~age_groups {*})
        Got it, $1 
        $cinema/customer_age=$1
        ^gotoReactivate(get_genre)

    u1: (*)
        Sorry, I didn't get that. Retry.
        ^stayInScope
        
# --- Get Movie Genre Preference ---
proposal: %get_genre
    What movie genre do you like? 

    u1: ({*} _~movie_genres {*})
        $cinema/movie_preference=$1
        $cinema/function = register_customer
        ^gotoReactivate(main_hub)

    u1: (*)
        Sorry, I didn't recognize that genre. Retry.
        ^stayInScope

proposal: %update_preferences
    What movie genre do you like? 
    u1: ({*} _~movie_genres {*})
        Ok, I will update it.
        $cinema/movie_preference=$1
        $cinema/function=update_preferences
        ^gotoReactivate(main_hub)
        
    u1: (*)
        Sorry, I didn't recognize that genre. Retry.
        ^stayInScope

# --- Offer Movie Recommendation ---
proposal: %offer_recommendation
    $cinema/function=recommend_movies
    u1: (e:cinema/movie_suggestions)
        ^gotoReactivate(choose_movie_from_recommendations)

# --- Choose a Movie given a Recommendation ---
proposal: %choose_movie_from_recommendations
    Here some movies you could like: $cinema/movie_suggestions .
    Do any of these sound good? Just tell me which one you’d like to know more about.

    u1: (~refusal)
        No problem! Let me know if you need anything else.
        ^gotoReactivate(main_hub)

    u1: (_*)
        $cinema/selected_movie=$1
        $cinema/function=get_description
        I'll get the description for $1

        u2: (e:cinema/description_failed)
            $cinema/description_failed . 
            ^previousProposal

        u2: (e:cinema/description)
            $cinema/description
            Are you interested in this movie?

            u3: (~acceptance)
                $cinema/function=get_showtimes

                u4: (e:cinema/available_times)
                    ^gotoReactivate(booking_recommendation)

            u3: (~refusal)
                Ok!
                ^gotoReactivate(main_hub)

            u3: (*)
                Sorry, didn't understand, Are you interested in this movie ?
                ^stayInScope

proposal: %booking_recommendation
    Let's book your tickets, What time would you like to attend $cinema/selected_movie ? 
    We have:  $cinema/available_times

        u1: (~refusal)
            Ok! 
            ^gotoReactivate(main_hub)
            
        u1: (_*)
            $cinema/selected_time=$1
            $cinema/function=book_showtime
            Booking your tickets...

                u2: (e:cinema/booking_success)
                    $cinema/direction_request=box_office
                    $cinema/function=show_directions
                    Booking success! You can retrieve your tickets at the ticket office.
                    ^gotoReactivate(offer_screen_guidance)

                u2: (e:cinema/booking_failed)
                    Booking failed, encountered some errors. Retry again.
                    ^gotoReactivate(booking_recommendation)

proposal: %offer_screen_guidance
        Would you like me to guide you to the screen for $cinema/selected_movie ?

        u1: (~acceptance)
            Great, follow me.
            $cinema/function=guide_to_screen
            ^gotoReactivate(on_screen_arrival)

        u1: (~refusal)
            Alright! Enjoy your movie!
            ^gotoReactivate(main_hub)

        u1: (*)
            Sorry, didn't understand. Retry.
            ^stayInScope

proposal: %on_guidance_complete
    u1: (e:cinema/guidance_complete)
        We have arrived at the $cinema/target_location . Is there anything else I can help you with?
        ^gotoReactivate(main_hub)
    u1: (e:cinema/guidance_failed)
        Sorry, I couldn't guide you there. Let me know if you need help with something else.
        ^gotoReactivate(main_hub)

proposal: %on_screen_arrival
    u1: (e:cinema/screen_guidance_complete)
        We have arrived at screen $cinema/screen_guidance_complete  Enjoy the movie!
        ^gotoReactivate(main_hub)
    u1: (e:cinema/screen_guidance_failed)
        Sorry, I couldn't find the screen information. Let me know if you need help with something else.
        ^gotoReactivate(main_hub)

proposal: %booking
    # Se il film non è stato scelto
    $cinema/function = list_all_movies
    u1:(e:cinema/all_movies_list)
        We have $cinema/all_movies_list , which movie would you like to book tickets for?

        u2: (~refusal)
            Ok!
            ^gotoReactivate(main_hub)

        u2: (_*)
            #Qui se è scritto male, ci sono errori
            $cinema/selected_movie=$1
            $cinema/function=get_showtimes
            
            u3: (e:cinema/available_times)
                ^gotoReactivate(booking_recommendation)
            
            u3: (e:cinema/showtimes_failed)
                $cinema/showtimes_failed . 
                ^previousProposal

# --- Concessions Info ---
proposal: %buy_concession
    u1: (i want _*) 
        Adding $1 to your order ...
        $cinema/selected_concession=$1
        $cinema/function=add_to_order

        u2:(e:cinema/concession_found $cinema/concession_found==True)
            Item correctly added
            Do you want something else?
            ^gotoReactivate(buy_concession)

        u2:(e:cinema/concession_found $cinema/concession_found==False)
            Item doesen't exists. Retry.
            ^gotoReactivate(buy_concession)

    u1: (~done)
        $cinema/function=finalize_order
        u2: (e:cinema/order_complete)
            You ordered $cinema/complete_order 
            Your total is $cinema/order_total Please pay at the concession stand.
            ^gotoReactivate(main_hub)

        u2: (e:cinema/order_error)
            Your order is empty. Retry.
            ^gotoReactivate(buy_concession)

    u1: (~refusal)
        Ok, cancelling your order.
        $cinema/function=cancel_order
        ^gotoReactivate(main_hub)

    u1: (*)
        I didn't understand your order, please order again
        ^stayInScope

proposal: %main_hub
    How can I help you? You can ask for movie recommendations, showtimes, directions, concessions, or update your preferences.

    u1: ({*} _~update_prefs {*}) 
        Of course. Let's update your preferences.
        ^gotoReactivate(update_preferences)
   
    u1: ({*} _~ask_recommendation {*}) 
        I'd be happy to find a movie for you.
        ^gotoReactivate(offer_recommendation)

    u1: (~ask_showtimes)
        I can get you the showtimes. Which movie are you interested in?
        ^gotoReactivate(booking)
        
    u1: ({~ask_directions} {*} _~directions{*})
        I'll point you to the $1 right away!
        $cinema/direction_request=$1
        $cinema/function=show_directions
        u2: (e:cinema/already_there)
            $cinema/already_there 
            ^gotoReactivate(main_hub)
            
        u2: (e:cinema/direction_indication)
            $cinema/direction_indication
            Would you like me to guide you there?
            u3: (~acceptance)
                Great, follow me.
                $cinema/target_location=$cinema/direction_request
                $cinema/function=guide_to_location
                ^gotoReactivate(on_guidance_complete)

            u3: (~refusal)
                Alright! Enjoy your movie!
                ^gotoReactivate(main_hub)

            u3: (*)
                Sorry, i didn't understand. Retry with yes or no.
                ^stayInScope
    
    u1: (~goodbye)
        Thank you for visiting!
        $cinema/function = restart
        u1: (e:cinema/restart)
            ^gotoReactivate(restart)

    u1: (~ask_concessions)
        $cinema/function=concession_info
        u2: (e:cinema/concession_list)
            $cinema/concession_list
            ^gotoReactivate(buy_concession)
        
    u1: (*)
        Sorry, I can't help with that. I can provide movie recommendations, showtimes, directions, and concession information. What would you like to do?
        ^stayInScope


# --- Fallback & Clarifications ---
u: (*)
    Sorry Fatal error, I didn't quite get that. Now you can restart.
    ^gotoReactivate(restart)

   

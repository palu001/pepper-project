topic: ~cinema_assistant()
language: enu

# --- Concepts ---
concept: (myname) ["my name is" "i'm" "i am" "call me" "they call me" "you can call me" "i go by"]
concept: (greetings) ^rand["hi" "hello" "hello there" "hey there" "good morning" "hey" "hi there" "good afternoon" "good evening" "greetings" "good day"]
concept: (goodbye) ["goodbye" "bye" "farewell" "see you later" "take care"]
concept: (acceptance) ["yes" "yeah" "yep" "ok" "sure" "of course" "sounds good" "alright"]
concept: (refusal) ["no" "nope" "nah" "not now" "maybe later" "i'm good"]
concept: (movie_genres) ["action" "comedy" "drama" "horror" "romance" "sci-fi" "science fiction" "thriller" "documentary" "animated" "family" "adventure" "fantasy"]
concept: (age_groups) ["child" "teen"  "adult" "senior"]
concept: (directions) ["bathroom" "restroom" "washroom" "screen 1" "screen 2" "screen 3" "screen 4" "screen 5" "screen 6" "screen 7" "screen 8" "theater" "auditorium" "concession" "concession stand" "exit" "entrance" "box office" "ticket office"]
concept: (concessions) ["popcorn" "soda" "drink" "beverage" "nachos" "candy" "sweets" "snack" "hot dog" "food"]
concept: (update_prefs) ["update" "preferences" "change" "info" "details" "profile"]
concept: (ask_recommendation) ["recommend" "watch" "recommendations" "recommendation" "see" "tips" "tip"]
concept: (ask_showtimes) ["showtimes" "movie times" "what time are the movies playing" "get showtimes" "what's playing"]
concept: (ask_directions) ["directions" "where" "get to" "where is"]
concept: (ask_concessions) ["concessions" "buy snacks" "order food" "what food do you have" "menu" "i'm hungry"]
concept: (done) ["i'm done" "i am done" "done" "all done" "that's it" "finished" "i'm finished" "i’m good now"]

# --- Global Greeting ---   
u: (~greetings)
    Hey there! Welcome to our cozy cinema, glad to have you!
    ^gotoReactivate(get_name)

# --- Restart --- 
proposal: %restart
    u1: (~greetings) ^gotoReactivate(get_name)


# --- Get Name ---
proposal: %get_name
    What's your name?

    u1: ({~myname} _*)
        $cinema/customer_name=$1
        $cinema/function = greet_customer
        u2: (e:cinema/customer_identity_check $cinema/customer_identity_check==True) 
            Great! Welcome back, $cinema/customer_name
            $cinema/function = check_upcoming_showtime
            u3: (e:cinema/upcoming_showtime $cinema/upcoming_showtime==True)
                ^gotoReactivate(showtime_heads_up)
            u3: (e:cinema/upcoming_showtime $cinema/upcoming_showtime==False)
                ^gotoReactivate(use_tablet)
        u2: (e:cinema/customer_identity_check $cinema/customer_identity_check==False) 
                Nice to meet you, $cinema/customer_name! Let's get to know you better.
                ^gotoReactivate(get_age)


proposal: %showtime_heads_up
    Heads up! Your movie $cinema/upcoming_movie_title starts in just 10 minutes! Want me to guide you to Screen $cinema/screen_number ?

    u1: (~acceptance)
        Great, follow me.
        $cinema/selected_movie = $cinema/upcoming_movie_title
        $cinema/function=guide_to_screen
        ^gotoReactivate(on_screen_arrival)

    u1: (~refusal)
        Alright! Enjoy your movie and let me know next time if you liked it!
        $cinema/function = restart
        u2: (e:cinema/restart)
            ^gotoReactivate(restart)

    u1: (*)
        I might’ve missed that, do you want help getting to your screen?
        ^stayInScope

proposal: %check_feedback
    $cinema/function=check_for_feedback
    u1: (e:cinema/feedback_needed $cinema/feedback_needed==True) 
        ^gotoReactivate(get_movie_feedback)
    u1: (e:cinema/feedback_needed $cinema/feedback_needed==False) 
        Based on your preferences, here are one or more movies for you that are playing today, hurry up and book the tickets!!
        ^gotoReactivate(offer_recommendation)

proposal: %use_tablet
    Do you want to use the tablet in order to communicate?
    u1: (~acceptance)
        ^gotoReactivate(check_feedback_tablet)
    u1: (~refusal)
        ^gotoReactivate(check_feedback)

# --- Get Age Group ---
proposal: %get_age
    How would you describe your age group? Child, teen, adult, or senior?

    u1: ({*} _~age_groups {*})
        Got it, $1 
        $cinema/customer_age=$1
        ^gotoReactivate(get_genre)

    u1: (*)
        Hmm, I didn’t catch that. Mind trying again?.
        ^stayInScope
        
# --- Get Movie Genre Preference ---
proposal: %get_genre
    Just one more thing, what kind of movie gets you excited? Action, comedy, sci-fi name your vibe!

    u1: ({*} _~movie_genres {*})
        $cinema/movie_preference=$1
        $cinema/function = register_customer
        ^gotoReactivate(use_tablet)

    u1: (*)
        Oops, that genre doesn’t ring a bell. Can you try another one?
        ^stayInScope



# --- NEW: Post-Movie Feedback ---
proposal: %get_movie_feedback
    You saw $cinema/last_watched_movie last time, did you like it?
    u1: (~acceptance)
        $cinema/liked_status = True
        $cinema/function = record_feedback
        u2: (e:cinema/feedback_response)
            $cinema/feedback_response
            Since you've liked $cinema/last_watched_movie , here are one or more movies for you that are playing today, hurry up and book the tickets!!
            ^gotoReactivate(offer_recommendation)
    u1: (~refusal)
        $cinema/liked_status = False
        $cinema/function = record_feedback
        u2: (e:cinema/feedback_response)
            $cinema/feedback_response
            Since you didn't like $cinema/last_watched_movie , here are one or more movies for you that are playing today, hurry up and book the tickets!!
            ^gotoReactivate(offer_recommendation)
    u1: (*) I didn't quite get that. Did you like the movie? Please answer with a yes or no. ^stayInScope




proposal: %update_preferences
    What movie genre do you like? 
    u1: ({*} _~movie_genres {*})
        Ok, I will update it.
        $cinema/movie_preference=$1
        $cinema/function=update_preferences
        ^gotoReactivate(main_hub)
        
    u1: (*)
        Sorry, I didn't recognize that genre. Retry.
        ^stayInScope

# --- Offer Movie Recommendation ---
proposal: %offer_recommendation
    $cinema/function=recommend_movies
    u1: (e:cinema/movie_suggestions)
        ^gotoReactivate(choose_movie_from_recommendations)

# --- Choose a Movie given a Recommendation ---
proposal: %choose_movie_from_recommendations
    $cinema/movie_suggestions 
    Do any of these sound good? Just tell me which one you’d like to know more about.

    u1: (~refusal)
        No problem! Let me know if you need anything else.
        ^gotoReactivate(main_hub)

    u1: (_*)
        $cinema/selected_movie=$1
        $cinema/function=get_description
        I'll get the description for $1

        u2: (e:cinema/description_failed)
            $cinema/description_failed .
            ^previousProposal

        u2: (e:cinema/description)
            $cinema/description
            Are you interested in this movie?

            u3: (~acceptance)
                $cinema/function=get_showtimes

                u4: (e:cinema/available_times)
                    ^gotoReactivate(booking_recommendation)

            u3: ( ~refusal)
                Ok!
                ^gotoReactivate(main_hub)

            u3: (*)
                Sorry, didn't understand, Are you interested in this movie ?
                ^stayInScope

proposal: %booking_recommendation
    Let's book your tickets, What time would you like to attend $cinema/selected_movie ? 
    We have:  $cinema/available_times
        u1: (~refusal)
            Ok! 
            ^gotoReactivate(main_hub)
            
        u1: (_*)
            $cinema/selected_time=$1
            $cinema/function=book_showtime
            Booking your tickets...

                u2: (e:cinema/booking_success)
                    $cinema/direction_request=box_office
                    $cinema/function=show_directions
                    All set! You can grab your tickets from the box office, it's marked on the tablet. Let me know when you're done, and I’ll help with the next step!

                    u3: (~done)
                        ^gotoReactivate(offer_screen_guidance)

                    u3: (~refusal)
                        Ok, You need to go take the tickets at the box office. Good movie.
                        ^gotoReactivate(main_hub)

                u2: (e:cinema/booking_failed)
                    Booking failed, encountered some errors. Retry again, maybe using the tablet could help us understand each other better
                    ^gotoReactivate(booking_recommendation)

proposal: %offer_screen_guidance
        Would you like me to guide you to the screen for $cinema/selected_movie ?

        u1: (~acceptance)
            Great, follow me.
            $cinema/function=guide_to_screen
            ^gotoReactivate(on_screen_arrival)

        u1: (~refusal)
            Alright! Enjoy your movie and let me know next time if you liked it!
            ^gotoReactivate(main_hub)

        u1: (*)
            Sorry, didn't understand. Retry.
            ^stayInScope

proposal: %on_guidance_complete
    u1: (e:cinema/guidance_complete)
        We have arrived at the $cinema/target_location .
        ^gotoReactivate(main_hub)
    u1: (e:cinema/guidance_failed)
        Sorry, I couldn't guide you there. Let me know if you need help with something else.
        ^gotoReactivate(main_hub)

proposal: %on_screen_arrival
    u1: (e:cinema/screen_guidance_complete)
        We have arrived at screen $cinema/screen_guidance_complete  Enjoy the movie! Inform me when you've finished watching it.
        u2: (~done)
            Have you enjoyed the movie?
            u3: (~refusal)
                $cinema/liked_status = False
                $cinema/function = record_feedback
                u3: (e:cinema/feedback_response)
                    $cinema/feedback_response
                    ^gotoReactivate(main_hub)
            u3: (~acceptance)
                $cinema/liked_status = True
                $cinema/function = record_feedback
                u3: (e:cinema/feedback_response)
                    $cinema/feedback_response
                    ^gotoReactivate(main_hub)
        u2: (~refusal)
            Fine, i'll ask you another time!
            ^gotoReactivate(main_hub)

    
    u1: (e:cinema/screen_guidance_failed)
        Sorry, I couldn't find the screen information. Let me know if you need help with something else.
        ^gotoReactivate(main_hub)

proposal: %booking
    # Se il film non è stato scelto
    $cinema/function = list_all_movies
    u1:(e:cinema/all_movies_list)
        We have $cinema/all_movies_list , which movie would you like to book tickets for?

        u2: (~refusal)
            Ok!
            ^gotoReactivate(main_hub)

        u2: (_*)
            #Qui se è scritto male, ci sono errori
            $cinema/selected_movie=$1
            $cinema/function=get_showtimes
            
            u3: (e:cinema/available_times)
                ^gotoReactivate(booking_recommendation)
            
            u3: (e:cinema/showtimes_failed)
                $cinema/showtimes_failed or maybe using the tablet could help us understand each other better. 
                ^gotoReactivate(booking)


# --- Concessions Info ---
proposal: %has_preferred
    $cinema/function = has_preferred
    u1: (e:cinema/preferred_list $cinema/preferred_list == True)
        ^gotoReactivate(buy_preferred)
    u1: (e:cinema/preferred_list $cinema/preferred_list == False)
        ^gotoReactivate(buy_concession)

proposal: %buy_preferred
    Hey, welcome back to the bar! I know you really like $cinema/preferred_item would you like to add it to cart?
    $cinema/function = preferred_buy
    u1: (~refusal)
        All right
        ^gotoReactivate(buy_concession)
    u1: (~acceptance)
        Adding $cinema/preferred_item to the cart
        $cinema/selected_concession=$cinema/preferred_item
        $cinema/function=add_to_order
        u2:(e:cinema/concession_found $cinema/concession_found==True)
            Item correctly added
            Do you want something else?
            ^gotoReactivate(buy_concession)

        u2:(e:cinema/concession_found $cinema/concession_found==False)
            Item doesen't exists. Retry.
            ^gotoReactivate(buy_concession)

proposal: %buy_concession

    $cinema/function=concession_info
    u1: (e:cinema/concession_list)
        $cinema/concession_list

        u2: (i want _*) 
            I'm adding $1 to your order ...
            $cinema/selected_concession=$1
            $cinema/function=add_to_order

            u3:(e:cinema/concession_found $cinema/concession_found==True)
                Item correctly added
                Do you want something else?
                ^gotoReactivate(buy_concession)

            u3:(e:cinema/concession_found $cinema/concession_found==False)
                It looks like we don't have this item. Would you mind trying with other items?.
                ^gotoReactivate(buy_concession)

        u2: ([
            ~done
            ~refusal
              ])
            $cinema/function=finalize_order
            u3: (e:cinema/order_complete)
                $cinema/direction_request=concession
                $cinema/function=show_directions
                You ordered $cinema/complete_order 
                That will be $cinema/order_total - just head over to the concession stand. Let me know once you’re done!
                u4: (~done)
                    ^gotoReactivate(main_hub)

            u3: (e:cinema/order_error)
                Your order is empty. Retry.
                ^gotoReactivate(buy_concession)

        u2: (cancel)
            Ok, cancelling your order.
            $cinema/function=cancel_order
            ^gotoReactivate(main_hub)

        u2: (_*)
            Adding $1 to your order ...
            $cinema/selected_concession=$1
            $cinema/function=add_to_order

            u3:(e:cinema/concession_found $cinema/concession_found==True)
                Item correctly added
                Do you want something else?
                ^gotoReactivate(buy_concession)

            u3:(e:cinema/concession_found $cinema/concession_found==False)
                Item doesen't exists. Retry.
                ^gotoReactivate(buy_concession)

proposal: %main_hub
    So, what can I do for you today? Want movie tips, snacks, showtimes, directions or maybe change preferences? I’ve got you covered!
    $cinema/function = main_hub
    u1: ({*} _~update_prefs {*}) 
        Of course. Let's update your preferences.
        ^gotoReactivate(update_preferences)
   
    u1: ({*} _~ask_recommendation {*}) 
        I'd be happy to find a movie for you.
        Based on your preferences, here are one or more movies for you that are playing today, hurry up and book the tickets!!
        ^gotoReactivate(offer_recommendation)

    u1: (~ask_showtimes)
        I can get you the showtimes. Which movie are you interested in?
        ^gotoReactivate(booking)
        
    u1: ({~ask_directions} {*} _~directions{*})
        I'll point you to the $1 right away!
        $cinema/direction_request=$1
        $cinema/function=show_directions
        u2: (e:cinema/already_there)
            $cinema/already_there 
            ^gotoReactivate(main_hub)
            
        u2: (e:cinema/direction_indication)
            $cinema/direction_indication
            Would you like me to guide you there?
            u3: (~acceptance)
                Great, follow me.
                $cinema/target_location=$cinema/direction_request
                $cinema/function=guide_to_location
                ^gotoReactivate(on_guidance_complete)

            u3: (~refusal)
                Alright! Let me know next time if you liked it.
                ^gotoReactivate(main_hub)

            u3: (*)
                Sorry, i didn't understand. Retry with yes or no.
                ^stayInScope
                
    u1: (~goodbye)
        Thank you for visiting!
        $cinema/function = restart
        u1: (e:cinema/restart)
            ^gotoReactivate(restart)

    u1: (~ask_concessions)
        ^gotoReactivate(has_preferred)
        
    u1: (*)
        Hmm, that’s a bit beyond my usual scope, but I can help with movies, snacks, directions, or your profile. What would you like to do?
        ^stayInScope
    
proposal: %tablet_concession
    Sure, here are the available concession items.
    $cinema/tablet = tablet_concessions

    u1: (e:cinema/tablet_cancel $cinema/tablet_cancel == True)
        Your order has been cancelled
        $cinema/tablet = tablet_cancel_order
        ^gotoReactivate(tablet_main_hub)

    u1: (e:cinema/tablet_add)
        Adding $cinema/selected_concession to your order...
        $cinema/tablet = tablet_add_to_order
        Item correctly added
        u2:(e:cinema/tablet_repeat $cinema/tablet_repeat==True)
            ^gotoReactivate(tablet_concession)
        u2:(e:cinema/tablet_repeat $cinema/tablet_repeat==False)
            $cinema/tablet = tablet_finalize_order
            u3: (e:cinema/tablet_order_complete)
                $cinema/direction_request=concession
                $cinema/tablet = tablet_show_directions_concession
                $cinema/complete_order 
                Your total is $cinema/order_total Please pay at the concession stand. I will show you on the tablet.
                u4:(e:cinema/tablet_directions_done)
                    ^gotoReactivate(tablet_main_hub)


    

proposal: %tablet_recommendations
    Here are some recommendations
    $cinema/tablet = tablet_offer_recommendation
    u1:(e:tablet/movie_suggestions $tablet/movie_suggestions == True)
        ^gotoReactivate(tablet_get_description)


proposal: %tablet_get_description
    Here is the description for $cinema/selected_movie
    $cinema/tablet = tablet_get_description
    u1:(e:tablet/recommend_cancel $tablet/recommend_cancel == True)
        ^gotoReactivate(tablet_main_hub)

    u1:(e:tablet/recommend_choice $tablet/recommend_choice == True)
        ^gotoReactivate(tablet_showtimes)


proposal: %tablet_list
    $cinema/tablet = tablet_list_all_movies
    Which movie would you like to book tickets for?
    u1:(e:tablet/cancel_choice $tablet/cancel_choice == True)
        ^gotoReactivate(tablet_main_hub)
    u1:(e:tablet/movie_selected $tablet/movie_selected == True)
        ^gotoReactivate(tablet_showtimes)

proposal: %tablet_showtimes
    Here you can see the available times of the show 
    $cinema/tablet = tablet_get_showtimes
    u1:(e:tablet/choose_time $tablet/choose_time == True)
        ^gotoReactivate(tablet_booking)

proposal: %tablet_booking

    $cinema/tablet = tablet_book_showtime
    u1: (e:tablet/booking_success $tablet/booking_success == True )
        $cinema/direction_request=box_office
        $cinema/tablet = tablet_show_directions_box
            u2:(e:cinema/tablet_directions_done)
                ^gotoReactivate(tablet_offer_screen_guidance)


proposal: %tablet_directions
    Ok, now you need to choose from the tablet where you want to go.
    $cinema/tablet = tablet_choose_directions
    u1: (e:cinema/tablet_directions_chosen $cinema/tablet_directions_chosen==True)
        Ok, I will show you on the tablet the directions for $cinema/direction_request
        $cinema/tablet = tablet_show_directions
        u2: (e:cinema/tablet_already_there)
            $cinema/tablet_already_there 
            ^gotoReactivate(tablet_main_hub)
            
        u2: (e:cinema/tablet_direction_indication)
            Would you like me to guide you there?
            $cinema/tablet = tablet_confirm_guide_to_location
            u3: (e:cinema/tablet_guide_to_location $cinema/tablet_guide_to_location==True)
                Great, follow me.
                $cinema/tablet=tablet_guide_to_location
                u4: (e:cinema/tablet_guidance_complete)
                We have arrived at the $cinema/direction_request 
                ^gotoReactivate(tablet_main_hub)

            u3: (e:cinema/tablet_guide_to_location $cinema/tablet_guide_to_location==False)
                Alright! Let me know next time if you liked it.
                ^gotoReactivate(tablet_main_hub)


proposal: %tablet_offer_screen_guidance
        Would you like me to guide you to the screen for $cinema/selected_movie ?
        
        $cinema/tablet = tablet_offer_screen_guidance
        u1: (e:tablet/screen_guidance $tablet/screen_guidance == True)
            Great, follow me.
            $cinema/tablet=tablet_guide_to_screen
            u2:(e:cinema/screen_guidance_complete)
                    We have arrived at screen $cinema/screen_guidance_complete  Enjoy the movie!
                    ^gotoReactivate(tablet_main_hub)

        u1: (e:tablet/screen_guidance $tablet/screen_guidance == False)
            All right, enjoy your movie and let me know next time if you liked it
            ^gotoReactivate(tablet_main_hub)



proposal: %tablet_genre
    Of course. Let's update your preferences. What movie genre do you like?
    $cinema/tablet = tablet_genre
    u1: (e:cinema/tablet_genre_selected $cinema/tablet_genre_selected==True)
        Ok, I update it.
        ^gotoReactivate(tablet_main_hub)

proposal: %get_movie_feedback_tablet
    $cinema/tablet = check_feedback_tablet
    u1: (e:tablet/feedback_movie )
        $cinema/liked_status = $tablet/feedback_movie
        $cinema/function = record_feedback
        $cinema/tablet=tablet_feedback_response
        u2: (e:tablet/feedback_response)
            ^gotoReactivate(tablet_recommendations)


proposal: %check_feedback_tablet
    $cinema/function=check_for_feedback
    u1: (e:cinema/feedback_needed $cinema/feedback_needed==True) 
        ^gotoReactivate(get_movie_feedback_tablet)
    u1: (e:cinema/feedback_needed $cinema/feedback_needed==False) 
        ^gotoReactivate(tablet_recommendations)


proposal: %tablet_main_hub
    How can I help you? You can ask for movie recommendations, showtimes, directions, concessions, or update your preferences.

    $cinema/tablet = tablet_main_hub
    u1: (e:cinema/tablet_main_hub $cinema/tablet_main_hub == tablet_concessions)
        ^gotoReactivate(tablet_concession)

    u1: (e:cinema/tablet_main_hub $cinema/tablet_main_hub == tablet_recommendations)
        ^gotoReactivate(tablet_recommendations)

    u1: (e:cinema/tablet_main_hub $cinema/tablet_main_hub == tablet_showtimes)
        ^gotoReactivate(tablet_list)

    u1: (e:cinema/tablet_main_hub $cinema/tablet_main_hub == tablet_directions)
        ^gotoReactivate(tablet_directions)

    u1: (e:cinema/tablet_main_hub $cinema/tablet_main_hub == tablet_restart)
        ^gotoReactivate(tablet_restart)

    u1: (e:cinema/tablet_main_hub $cinema/tablet_main_hub == tablet_genre)
        Of course. Let's update your preferences.
        ^gotoReactivate(tablet_genre)



proposal: %tablet_restart
    Thank you for visiting!
    $cinema/tablet = tablet_restart
    u1: (e:cinema/tablet_restart)
        ^gotoReactivate(restart)

# --- Fallback & Clarifications ---
u: (*)
    Sorry Fatal error, I didn't quite get that. Now you can restart.
    $cinema/function = restart
    u1: (e:cinema/restart)
        ^gotoReactivate(restart)

   
